!--------------------------------------------------
!- 3/2024
!--------------------------------------------------
1 REM GOPHER64/128  1200B 2.0+
2 REM UPDATED 03/02/2024 01:33A
5 POKE254,8:IFPEEK(186)>7THENPOKE254,PEEK(186)
10 SY=PEEK(65532):IFSY=61THENPOKE58,254:CLR
12 IFSY=34THENX=23777:POKEX,170:IFPEEK(X)<>170THENPRINT"<16k":STOP
15 OPEN5,2,0,CHR$(8):IFPEEK(65532)=34THENPOKE56,87:POKE54,87:POKE52,87
17 TB$=CHR$(7):P$="ok":POKE186,PEEK(254):BA=1200:XB=1200
20 CR$=CHR$(13):PRINTCHR$(14);:SY=PEEK(65532):POKE53280,254:POKE53281,246
30 CO$="{light blue}":IFSY=226THENML=49152:POKE665,73-(PEEK(678)*30):UM=ML+2048:XB=9600
35 IFSY=34THENML=22273:IFPEEK(ML)<>76THENCLOSE5:LOAD"pmlvic.bin",peek(254),1:RUN
38 IFSY=34THENPOKE36879,27:CO$=CHR$(31)
40 IFSY=226ANDPEEK(ML+1)<>209THENCLOSE5:LOAD"pml64.bin",PEEK(254),1:RUN
45 IFSY=226ANDUM>0ANDPEEK(UM+1)<>24THENCLOSE5:LOAD"up9600.bin",PEEK(254),1:RUN
50 S8=0:IFSY=61THENML=4864:POKE981,15:S8=PEEK(215)AND128:IFS8=128THENSYS30643
60 IFSY=61ANDPEEK(ML+1)<>217THENCLOSE5:LOAD"pml128.bin",PEEK(254),1:RUN
70 IFSY=61ANDS8=128THENXB=2400:CO$=CHR$(159)
80 IFSY=226ANDUM>0THENSYSUM:SYSUM+3:X=PEEK(789):SYSUM+9:IFX=234THENXB=1200
90 IFSY<>34THENPOKE56579,0:REM WHY DOES THIS WORK
100 MV=ML+18:POKEMV+14,8:DD=56577:IFSY=34THENDD=37136:REM FIX BUFAP
101 REM
102 REM
110 REM
120 PRINTCO$;"{clear}{down*2}GOPHER v1.8":PRINT"Requires Zimodem firmware 2.0+"
140 PRINT"By Bo Zimmerman (bo@zimmers.net)":PRINT:PRINT
199 REM ---- ZIMODEM SETUP
200 UN=PEEK(254):IP$="":CR$=CHR$(13)+CHR$(10)
201 PH=0:PT=0:MV=ML+18
202 PRINT "Initializing modem..."
203 GET#5,A$:IFA$<>""THEN203
205 PRINT#5,CR$;"athz0&p0f0e0";CR$;:GOSUB900:IFP$<>"ok"THEN203
208 GET#5,A$:IFA$<>""THEN208
210 PRINT#5,CR$;"ate0n0r0v1q0";CR$;
220 GOSUB900:IFP$<>"ok"THEN208
225 FORI=1TO100:NEXT
228 GET#5,A$:IFA$>""THEN228
230 PRINT#5,"ate0v1x1f3q0s40=248s0=1s41=0i4";CR$;CHR$(19);:L9=248
235 GOSUB900:VR=VAL(P$):IFVR<2.0THENPRINT"Zimodem init failed: ";P$:STOP
240 GOSUB900:IFP$<>"ok"THEN203
245 GET#5,A$:IFA$<>""THEN245
300 PRINT"Enter a URL, e.g. "
310 PRINT"     gopher://gopher.quux.org:70"
320 PRINT"URL: gopher://";:GOSUB5000:UR$=P$:IFUR$=""THENEND
400 DIMS$(30):S$(0)="":SP=0
500 GOTO1000:REM -- GO BEGIN!
598 REM --- TRANSMIT P$ TO THE OPEN SOCKET !
600 OP$=P$:SYSML+9:C8$=MID$(STR$(PEEK(MV+8)),2):E$="ok":IFVR>3THENE$=C8$
610 PRINT#5,"ats42=";C8$;"tp+";QU$;P$;QU$
620 SYSML:IF(PEEK(DD)AND16)=0THENPRINT"{red}Lost connection";CO$:RETURN
625 IFP$<>E$THENPRINT"{reverse on}{red}xerr:{reverse off}";CO$;P$:P$=OP$:GOTO600
630 RETURN
650 OP$=P$:SYSML+9:C8$=MID$(STR$(PEEK(MV+8)),2):PN$=MID$(STR$(LEN(P$)),2)
660 PRINT#5,"ats42=";C8$;"t+";PN$:PRINT#5,P$:E$="ok":IFVR>3THENE$=C8$
670 SYSML:IF(PEEK(DD)AND16)=0THENPRINT"{red}Lost connection";CO$:RETURN
675 IFP$<>E$THENPRINT"xerr:";P$:P$=OP$:GOTO650
680 RETURN
798 REM --- GET P$ FROM SOCKET P
800 P$="":E=0
810 GOSUB930:IFP0<>PANDP0<0THENPRINT"Unexpected packet id: ";P0;"/";P:STOP
820 IFP0=0THENE=1:RETURN:REM FAIL
830 RETURN
850 PC=0
860 PC=PC+1:GOSUB800:IFP$=""ORE=1THENIFPC<60THEN860
870 IFLEN(P$)<4ORMID$(P$,4,1)<>"-"THENRETURN
880 EC$=LEFT$(P$,3):EC=VAL(EC$)
881 PC=0
882 GOSUB800:IFP$=""ORE=1THENIFPC<60THEN882
883 IFP$=""ORE=1THEN870
884 IFLEFT$(P$,3)<>EC$THENPRINTP$:GOTO881
885 IFMID$(P$,4,1)="-"THEN881
886 RETURN
898 REM --- GET E$ FROM MODEM, OR ERROR
900 E$=""
910 SYSML
920 IFE$<>""ANDP$<>E$THENPRINT"{reverse on}{red}Comm error. Expected ";E$;", Got ";P$;CO$;"{reverse off}"
925 RETURN
927 REM ---- LOW LVL PACKET READ
930 PR=0:GET#5,P$:IFP$<>""THEN930
940 PRINT#5,CHR$(17);
945 SYSML+6:P0=PEEK(MV+2):P1=PEEK(MV+4):P2=PEEK(MV+6)
950 PL=PEEK(MV+0):CR=PEEK(MV+1):C8=PEEK(MV+8)
960 IFP0>0ANDP2<>C8THEN985
970 IFP1=0THENP$=""
980 IFP0>0ORCR=0THENRETURN
985 GET#5,P$:IFP$<>""THEN985
990 PRINT"{yellow}PACKET-RETRY";CO$:PRINT#5,"atl":GOTO945
995 PRINT"Expected ";E$;", got ";A$:STOP
998 REM --- THE MAIN LOOP
1000 QU$=CHR$(34):REM BEGIN!
1010 AT$="":IFXB>0ANDBA>0ANDXB<>BATHENAT$="s43="+MID$(STR$(XB),2)
1020 GET#5,A$:IFA$<>""THEN1020
1050 IFVAL(RIGHT$(UR$,1))=0ANDRIGHT$(UR$,1)<>"0"THENUR$=UR$+":70"
1100 PRINT#5,"ath"+AT$+"&d10&m13&m10cp";QU$;UR$;QU$:E=0
1105 GOSUB900:SP=0:IFP$="ok"THEN1020
1110 IFLEN(P$)>8ANDLEFT$(P$,8)="connect "THENP=VAL(MID$(P$,9)):GOTO1200
1112 IFLEN(P$)=0ANDE<3THENE=E+1:GOTO1105
1115 PRINT"{pink}{reverse on}Unable to connect to ";UR$;"{reverse off}";CO$:GOTO300
1120 IT=TI+40
1130 P9=0:GOSUB800:IFP$<>""THENIT=TI+10
1140 IFTI<ITTHEN1130
1200 PRINT"{reverse on}{light green}Connected to ";UR$;" on channel";P;", Reading...{reverse off}";CO$
1201 IFXB<>9600THEN1205
1202 SYSUM:SYSUM+3:IFPEEK(789)=234THENSYSUM+9:GOTO1210
1203 BA=XB:POKEUM+19,1:PRINT#5,"at":GOSUB9000:GOSUB9000
1205 IFXB<>2400THEN1210
1206 NP=0:IFPEEK(2614)>0THENNP=20
1207 BA=XB:POKE2576,10:POKE2578,PEEK(59490+NP):POKE2579,PEEK(59491+NP)
1208 POKE2582,170:POKE2583,1:IFNP>0THENPOKE2582,154
1210 PRINT#5,"at":GOSUB9000:GOSUB9000
1220 GOSUB850:IFP$=""THEN1240
1250 DIMSC$(100):DIMSC(50):REM top,bot,showtop,numlink,
1299 REM -- REQUEST PAGE 
1300 P$=S$(SP):GOSUB600:FORI=0TO50:SC(I)=0:NEXT
1310 IFPEEK(DD)AND16=0THEN1400
1320 GOSUB930:IFP$>""thenSC$(SC(1))=P$:SC(1)=SC(1)+1:IFSC(1)>100THEN1400
1400 REM CLOSE UP, YER DONE
1410 PRINT#5,"ATH":GOSUB9000
2000 

9000 TT=TI+100
9010 SYSML+12:IFTI<TTTHEN9010
9020 RETURN
9200 REM -- TABERATE
9210 X=1:I=1:PN=0
9220 PN$(PN)="":
9230 IFMID$(P$,I,1)=TB$ANDI>XTHENPN$(PN)=MID$(P$,X,I-X):PN=PN+1:X=I+1:GOTO9220
9240 I=I+1:IFI<LEN(P$)ANDPN<11THEN9230
9250 RETURN
50000 OPEN5,2,0,CHR$(8)
50010 GET#5,A$:IFA$<>""THENPRINTA$;
50020 GETA$:IFA$<>""THENPRINT#5,A$;
50030 GOTO 50010
55555 U=8:F$="gopher":OPEN1,U,15,"s0:"+F$:CLOSE1:SAVE(F$),U:VERIFY(F$),U
55565 